= Importing Git History into Neo4j
:presenter: Michael Hunger
:twitter: mesirii
:email: michael@neotechnology.com
:backend: deckjs
:deckjsdir: ../../../asciidoc/deck.js
:deckjs_theme: neotech
:icons: font
:source-highlighter: codemirror
:navigation:
:goto:
:menu:
:status:
:arrows:
:customjs: ../../../asciidoc/js/checkcypher.js
:gist-source: https://raw.github.com/neo4j-contrib/gists/master/
:footer: © All Rights Reserved 2013 | Neo Technology, Inc.
:img: img
:logo: {img}/Neo_Technology.jpg
:allow-uri-read:
:video:
:docs-link: https://github.com/neo4j-contrib/asciidoc-slides[documentation]
:download-link: https://github.com/neo4j-contrib/asciidoc-slides/archive/master.zip[download]
:sectids!:
:deckjs_transition: none

++++
<style type="text/css">
.small {
   font-size:0.6em;
}
.big {
   font-size:1.2em;
}
.center {
   position:absolute;
   left: 20%;
   top: 20%;
   bottom: 20%;
}
table, td {
  border: none;
}
</style>
++++


== Who the hell is this guy?

* Michael Hunger
* Developer Evangelist Neo Technology
* Addicted to Graphs and helping People
* @mesirii | michael@neotechnology.com


== What will he talk about?

[options="step"]
* Importing at git log into Neo4j 
* Compare two approaches
* Generating Cypher statements
* LOAD CSV

== Question for you!

[options="step"]
* Have you used NoSQL DB's? Yes!
* Have you used Graph Databases?
* Have you used Neo4j?
* Have you used Neo4j before 2.1?

== It started with

++++
<blockquote class="twitter-tweet" lang="en"><p>View your <a href="https://twitter.com/search?q=%23git&amp;src=hash">#git</a> commit graph in <a href="https://twitter.com/neo4j">@neo4j</a> with this (q&amp;d) <a href="https://twitter.com/search?q=%23python&amp;src=hash">#python</a> script: <a href="http://t.co/ToPFxYO08a">http://t.co/ToPFxYO08a</a></p>&mdash; Paul Horn (@knutwalker) <a href="https://twitter.com/knutwalker/statuses/470277723106344960">May 24, 2014</a></blockquote>
<script async src="//platform.twitter.com/widgets.js" charset="utf-8"></script>
++++

https://gist.github.com/knutwalker/d0ef536ec6ceafba570a

=== Thank you Paul!

== The approach

* Use git log formatted output +
  git log --format:format="'%H'\t'%ai'\t'%at'"
* one big cypher statement
* use identifiers to connect commits to their parents

== Data Model

++++
<figure class="graph-diagram">
<ul class="graph-diagram-markup" data-internal-scale="2.63" data-external-scale="0.75">
  <li class="node" data-node-id="0" data-x="-266.9168926420667" data-y="-51.22776031494141">
    <span class="caption">Commit</span><dl class="properties"><dt>sha1</dt><dd>3438789sdf98s</dd><dt>subject</dt><dd>"I did this"</dd><dt>author_email</dt><dd>foo@bar.com</dd><dt>author_name</dt><dd>"Foo Bar"</dd></dl></li>
  <li class="node" data-node-id="1" data-x="-56.15291400959831" data-y="-79.67004778003988">
    <span class="caption">Commit</span>
  </li>
  <li class="node" data-node-id="2" data-x="104.10864487675134" data-y="-206.32936951269312">
    <span class="caption">Commit</span>
  </li>
  <li class="node" data-node-id="3" data-x="-5.9914297455789995" data-y="-267.2061829685237">
    <span class="caption">Commit</span>
  </li>
  <li class="relationship" data-from="0" data-to="1">
    <span class="type">PARENT</span>
  </li>
  <li class="relationship" data-from="1" data-to="2">
    <span class="type">PARENT</span>
  </li>
  <li class="relationship" data-from="1" data-to="3">
    <span class="type">PARENT</span>
  </li>
</ul>
</figure>
++++

NOTE: Not very graphy yet.

== Script Cypher Output

[source,cypher,role=big]
----
CREATE (c_0d952d4bb4c497146057ba28cf000bd0443902cd:Commit {author_email:'github@jexp.de',date_iso_8601:'2014-05-16 13:43:05 +0200',parents:['cfd697ee54108ba0e16da296f6d33f230e8bb4a2'],refs:['HEAD', 'origin/master', 'origin/gh-pages', 'origin/HEAD', 'master', 'gh-pages'],sha1:'0d952d4bb4c497146057ba28cf000bd0443902cd',subject:'presentation from jax',timestamp:'1400240585'})
CREATE (c_cfd697ee54108ba0e16da296f6d33f230e8bb4a2:Commit {author_email:'github@jexp.de',date_iso_8601:'2014-05-11 19:43:41 +0200',parents:['edf81fde813d3feedd9b12af12dd3ee145d9ff78', '31a6f819eaef8c704088838340a26fd8b7babd00'],refs:['x'],sha1:'cfd697ee54108ba0e16da296f6d33f230e8bb4a2',subject:'merge',timestamp:'1399830221'})
CREATE (c_31a6f819eaef8c704088838340a26fd8b7babd00:Commit {author_email:'github@jexp.de',date_iso_8601:'2014-05-11 19:25:54 +0200',parents:['df20f18c13a29e563d96c6c95b9f77930699f6bc'],refs:['mh'],sha1:'31a6f819eaef8c704088838340a26fd8b7babd00',subject:'renamed fosdem presentation to graphgist',timestamp:'1399829154'})
CREATE (c_edf81fde813d3feedd9b12af12dd3ee145d9ff78:Commit)
CREATE (c_df20f18c13a29e563d96c6c95b9f77930699f6bc:Commit)
CREATE
  (c_0d952d4bb4c497146057ba28cf000bd0443902cd)<-[:PARENT]-(c_cfd697ee54108ba0e16da296f6d33f230e8bb4a2),
  (c_cfd697ee54108ba0e16da296f6d33f230e8bb4a2)<-[:PARENT]-(c_edf81fde813d3feedd9b12af12dd3ee145d9ff78),
  (c_cfd697ee54108ba0e16da296f6d33f230e8bb4a2)<-[:PARENT]-(c_31a6f819eaef8c704088838340a26fd8b7babd00),
  (c_31a6f819eaef8c704088838340a26fd8b7babd00)<-[:PARENT]-(c_df20f18c13a29e563d96c6c95b9f77930699f6bc)
RETURN c_0d952d4bb4c497146057ba28cf000bd0443902cd;
----

NOTE: One Big Statement

== Run the Import

[source,bash]
----
time ~/Downloads/neo4j-community-2.1.0-RC2/bin/neo4j-shell -path git.db -file git.cql -q

Transaction started
+-------------------+
| No data returned. |
+-------------------+
Nodes created: 1
Properties set: 9
Labels added: 1
94 ms

....

+-------------------+
| No data returned. |
+-------------------+
Relationships created: 1
3 ms
Transaction committed

real	3m23.570s
user	3m28.064s
sys	0m7.741s
----

== I don't know Python

but did some changes anyway:

* one cypher statement per line
* fixing non US-ASCII characters
* more commit attributes
* graph-refactoring
* visualize & style in Neo4j browser

[canvas-image="{img}/git2neo.jpg"]
== So we were got here

=== So we were got here

[role=center]
++++
<blockquote class="twitter-tweet" lang="en"><p>Playing around with git log data of neo4j in <a href="https://twitter.com/neo4j">@Neo4j</a>. Thanks again <a href="https://twitter.com/knutwalker">@knutwalker</a> for the script. <a href="https://t.co/u6PjgXPHxH">https://t.co/u6PjgXPHxH</a> <a href="http://t.co/C39DvHzGur">pic.twitter.com/C39DvHzGur</a></p>&mdash; Michael Hunger (@mesirii) <a href="https://twitter.com/mesirii/statuses/470692670202191873">May 25, 2014</a></blockquote>
<script async src="//platform.twitter.com/widgets.js" charset="utf-8"></script>
++++

https://gist.github.com/jexp/844cf610db4951308574

== Script Cypher Output 

[source,cypher,role=big]
----
BEGIN
create constraint on (c:Commit) assert c.sha1 is unique;
COMMIT
BEGIN
CREATE (:Commit {author_email:'foo@bar.com',date_iso_8601:'2014-05-22 20:53:05 +0200',parents:['b6393fc9d5c065fd42644caad600a9b7ac911ae2'],refs:['HEAD', 'origin/master', 'master', 'in-index'],sha1:'934cacf9fe6cd0188be642b3e609b529edaad527',subject:'Some commit message',timestamp:'1400784785'});

CREATE (:Commit {author_email:'bar@foo.com',date_iso_8601:'2014-05-22 13:22:10 +0200',parents:['7765539ff17310f2c736ee7f0a8fc5e05180e262', '2d3abe010c36214b71c9bbbcaa9f6063947068de'],sha1:'b6393fc9d5c065fd42644caad600a9b7ac911ae2',subject:'Merge pull request #2445 from foo/bar',timestamp:'1400757730'});
...
MATCH (parent:Commit {sha1:"934cacf9fe6cd0188be642b3e609b529edaad527"}), 
      (child:Commit {sha1:"b6393fc9d5c065fd42644caad600a9b7ac911ae2"}) 
CREATE (parent)<-[:PARENT]-(child);
...
COMMIT
----

One statement per line

== Refactoring

=== Create Users

[source,cypher,role=big]
----
create constraint on (u:User) assert u.email is unique;
create index on :User(name);
 
MATCH (c:Commit)
MERGE (a:Author:User {email:c.author_email}) ON CREATE SET a.name = c.author_name
CREATE (a)-[:AUTHORED]->(c);
----

== Data Model

++++
<figure class="graph-diagram">
<ul class="graph-diagram-markup" data-internal-scale="2.63" data-external-scale="0.5">
  <li class="node" data-node-id="0" data-x="-414.7326230959642" data-y="-44.91645827492838">
    <span class="caption">Commit</span><dl class="properties"><dt>sha1</dt><dd>3438789sdf98s</dd><dt>subject</dt><dd>"I did this"</dd></dl></li>
  <li class="node" data-node-id="1" data-x="-56.15291400959831" data-y="-79.67004778003988">
    <span class="caption">Commit</span>
  </li>
  <li class="node" data-node-id="2" data-x="104.10864487675134" data-y="-206.32936951269312">
    <span class="caption">Commit</span>
  </li>
  <li class="node" data-node-id="3" data-x="104.10864487675134" data-y="15.547644666802626">
    <span class="caption">Commit</span>
  </li>
  <li class="node" data-node-id="4" data-x="-507.38871364811047" data-y="-206.32936951269312">
    <span class="caption">User</span><dl class="properties"><dt>name</dt><dd>"Foo Bar"</dd><dt>email</dt><dd>"foo@bar.com"</dd></dl></li>
  <li class="node" data-node-id="5" data-x="-179.80228792487887" data-y="-153.21541889901388">
    <span class="caption">Day</span><dl class="properties"><dt>date</dt><dd>"2014-05-26"</dd><dt>day</dt><dd>26</dd></dl></li>
  <li class="node" data-node-id="6" data-x="-282.5550212207402" data-y="-206.32936951269312">
    <span class="caption">Month</span><dl class="properties"><dt>month</dt><dd>05</dd></dl></li>
  <li class="node" data-node-id="7" data-x="-205.8444828860205" data-y="-304.7478132936894">
    <span class="caption">Year</span><dl class="properties"><dt>year</dt><dd>2014</dd></dl></li>
  <li class="relationship" data-from="0" data-to="1">
    <span class="type">PARENT</span>
  </li>
  <li class="relationship" data-from="1" data-to="2">
    <span class="type">PARENT</span>
  </li>
  <li class="relationship" data-from="1" data-to="3">
    <span class="type">PARENT</span>
  </li>
  <li class="relationship" data-from="4" data-to="0">
    <span class="type">AUTHORED</span>
  </li>
  <li class="relationship" data-from="1" data-to="5"></li>
  <li class="relationship" data-from="5" data-to="6"></li>
  <li class="relationship" data-from="6" data-to="7"></li>
</ul>
</figure>
++++

=== Create Time Tree (day level)

[source,cypher,role=big]
----
create constraint on (y:Year) assert y.year is unique;

MATCH (c:Commit)
MERGE (y:Year {year:substring(c.date,0,4)})
MERGE (m:Month {month:substring(c.date,5,2)})-[:IN_YEAR]->(y)
MERGE (d:Day {date:c.date, day:substring(c.date,8,2)})-[:IN_MONTH]->(m)
CREATE (c)-[:ON_DAY]->(d);
----

== Mark and rubber ducks

[options="step"]
* Talked to http://twitter.com/markhneedham[Mark Needham] about it
* I **realized**, the git log format is actually CSV
* So "LOAD CSV #FTW"
* It's soo much easier

== LOAD CSV Syntax

[source,cypher,role=big]
----
 [USING PERIODIC COMMIT [10000]]
LOAD CSV [WITH HEADERS] FROM "http://friends.csv" AS line
MATCH (me:User {name:line.user})
CREATE (me)-[:FRIEND]->(friend:User {name: line.friend})
RETURN COUNT(*);
----

== The secret of LOAD CSV

=== ETL Powertool

[options=step]
* combines multiple aspects in **a single operation**
* loading / ingesting CSV data from an URI
* direct mapping in complex graph/domain structure
* data conversion
* supports complex computations
* create or merge data, relationships and structure

== Generate CSV

[cols=".^,.<,.^,.<"]
|===
| What | Format | What | Format
| sha1 | %H | hash | %h
| parents | %P | refs | %d
| author_email | %ae | author_name | %an
| timestamp | %at | date_time | %ai (ISO)
| subject | %s
|===

[source,bash,role=big,options=step]
----
echo sha1,hash,parents,author_email,author_name,refs,subject,timestamp,date_time > ~/DropBox/Public/git.csv

git log --reverse --format='format:"%H","%h","%P","%ae","%an","%d", "%s","%at","%ai"' >> ~/DropBox/Public/git.csv
----

== Example Content

[source,csv]
----
sha1,hash,parents,autor_email,author_name,refs,subject,timestamp,date_time
"a9caf3f600c1c63aa8cc40db59bac53cabca2e50","a9caf3f","934cacf9fe6cd0188be642b3e609b529edaad527","github@jexp.de","Michael Hunger"," (HEAD, jexp/in-index, in-index)","Index Support for IN Predicates","1401011766","2014-05-25 11:56:06 +0200"
"934cacf9fe6cd0188be642b3e609b529edaad527","934cacf","b6393fc9d5c065fd42644caad600a9b7ac911ae2","andres@neotechnology.com","Andres Taylor"," (origin/master, master)","Typo","1400784785","2014-05-22 20:53:05 +0200"
"b6393fc9d5c065fd42644caad600a9b7ac911ae2","b6393fc","7765539ff17310f2c736ee7f0a8fc5e05180e262 2d3abe010c36214b71c9bbbcaa9f6063947068de","magnus.vejlstrup@neopersistence.com","magnusvejlstrup","","Merge pull request #2445 from jexp/batch-inserter-done","1400757730","2014-05-22 13:22:10 +0200"
"2d3abe010c36214b71c9bbbcaa9f6063947068de","2d3abe0","7765539ff17310f2c736ee7f0a8fc5e05180e262","github@jexp.de","Michael Hunger"," (jexp/batch-inserter-done)","fix bug in batch-insertion, CalculateDenseNodesStep. No call to super to shut down the Executor","1400757147","2014-05-22 13:12:27 +0200"
"7765539ff17310f2c736ee7f0a8fc5e05180e262","7765539","ff1177cb989eb78084ebcdcd73b92dd26088d7b4","andres@neotechnology.com","Andres Taylor","","Fix unit test so it runs predictably","1400750553","2014-05-22 11:22:33 +0200"
"ff1177cb989eb78084ebcdcd73b92dd26088d7b4","ff1177c","7172b752f0d3c10916a7b0f0e46000a350748c16 28b8b87201e11e9456e0c1ed6cf466186524cb77","lassewesth@gmail.com","Lasse Westh-Nielsen","","Merge pull request #2438 from benbc/2.1-more-udc-stats","1400748892","2014-05-22 10:54:52 +0200"
"7172b752f0d3c10916a7b0f0e46000a350748c16","7172b75","3cdbfe347698b420bd093b6efe6e5c1dc2a73d61","rickardoberg@gmail.com","Rickard Öberg","","Updated Logback to 1.1.2","1400748717","2014-05-22 16:51:57 +0800"
"28b8b87201e11e9456e0c1ed6cf466186524cb77","28b8b87","3cdbfe347698b420bd093b6efe6e5c1dc2a73d61","ben@bridesmere.com","Ben Butler-Cole","","Add UDC stats for numbers of IDs in use in store.","1400668829","2014-05-21 11:40:29 +0100"
"3cdbfe347698b420bd093b6efe6e5c1dc2a73d61","3cdbfe3","86f576fa933f4b3210bde6577b27034633dc3ef3","lassewesth@gmail.com","Lasse Westh-Nielsen","","ensure launch script does not warn when using OpenJDK","1400671622","2014-05-21 13:27:02 +0200"
----

== Import with LOAD CSV - Indexes and Constraints
[source,cypher,role=big]
----
CREATE CONSTRAINT ON (c:Commit) ASSERT c.sha1 IS UNIQUE;
CREATE INDEX ON :Commit(hash);
CREATE INDEX ON :Commit(date);

CREATE CONSTRAINT ON (u:User) ASSERT u.email IS UNIQUE;
CREATE INDEX ON :User(name);

CREATE CONSTRAINT ON (y:Year) ASSERT y.year IS UNIQUE;
----

== Import with LOAD CSV - Load the Data
[source,cypher,role=big]
----
USING PERIODIC COMMIT 10000

LOAD CSV WITH headers FROM "https://dl.dropboxusercontent.com/u/14493611/git.csv" as line
CREATE (c:Commit {sha1: line.sha1, hash:line.hash, subject:line.subject,
    date_time:line.date_time, date:substring(line.date_time,0,10), timestamp:toInt(line.timestamp), 
    parents:split(line.parents," "), 
    refs:case when size(line.refs) > 0 then split(substring(line.refs,2,length(line.refs)-3),", ") else null end})

MERGE (u:User:Author {email:line.author_email}) ON CREATE SET u.name = line.author_name
CREATE (u)-[:AUTHORED]->(c)

MERGE (y:Year {year:substring(line.date_time,0,4)})
MERGE (m:Month {month:substring(line.date_time,5,2)})-[:IN_YEAR]->(y)
MERGE (d:Day {date:substring(line.date_time,0,10), day:substring(line.date_time,8,2)})-[:IN_MONTH]->(m)
CREATE (c)-[:ON_DAY]->(d)
WITH c,line
WHERE line.parents <> ""
UNWIND split(line.parents," ") as parent_sha1
MATCH (parent:Commit {sha1 : parent_sha1})
CREATE (c)-[:PARENT]->(parent)
;
----


[source]
----
Nodes created: 54451
Relationships created: 81390
Properties set: 217364
Labels added: 54588
108057 ms -> 1min 50 s
----

[canvas-image="{img}/blank.png"]
== That's it

++++
<h1>Thank You! Questions ?</h1>
++++
